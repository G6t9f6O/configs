set print pretty on
set print union on
set confirm on
set verbose off

set output-radix 10
set input-radix 10

set height 0
set width 0

set $SHOW_CONTEXT = 1
set $CONTEXTSIZE_CODE = 12


define context-on
    set $SHOW_CONTEXT = 1
    printf "Displaying of context is now ON\n"
end
document context-on
Enable display of context on every program break.
end


define context-off
    set $SHOW_CONTEXT = 0
    printf "Displaying of context is now OFF\n"
end
document context-off
Disable display of context on every program break.
end


define contextsize-code
    if $argc != 1
        help contextsize-code
    else
        set $CONTEXTSIZE_CODE = $arg0
    end
end
document contextsize-code
Set code window size to NUM lines.
Usage: contextsize-code NUM
end


define context
    set $context_i = $CONTEXTSIZE_CODE
    if($context_i > 0)
        x /i $pc - 8
        set $context_i--
    end
    while ($context_i > 0)
        x /i
        set $context_i--
    end
end
document context
Print context window, i.e. disassemble $pc
end


define hook-stop
    # this makes 'context' be called at every BP/step
    if ($SHOW_CONTEXT > 0)
        context
    end
end
document hook-stop
!!! FOR INTERNAL USE ONLY - DO NOT CALL !!!
end
